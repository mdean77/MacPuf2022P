      SUBROUTINE GSINV (PO2,PC2,O2CON,C2CON,PH,SAT)
C THIS SR REVERSES GASES AND BY AN OPTIMISED PROCESS OF
C SUCCESSIVE APPROXIMATION USES O2 AND CO2 CONTENTS TO
C COMPUTE THE RESPECTIVE PARTIAL PRESSURES
C---------- CHANGE IN COMMON BLOCK SIZE
        COMMON NTAB(104)
C.......
        COMMON Y1(13),TEMP,Y2(3),HB,PCV,Y3(23),DPH,Y4(40),PK
      DATA ERR,FACT/.01,1./
C FLAGS ICH1/3 SIGNIFY STATES IN APPROXIMATION OF PO2,
C ICH2/4= SIMILARLY FOR PCO2
C ICH1=0 MEANS PO2 IS WELL ENOUGH APPROXIMATED WITH ABS.ACCURACY
C 'ERR' IN O2CON
C ICH1=1 MEANS A BRACKETING PROCEDURE TO ESTABLISH BOUNDS ON THE
C PO2 VALUE IS IN PROGRESS
C ICH3=1 A BRACKET HAS BEEN ESTABLISHED AND A LINEAR INTERPOLATION
C MADE FOR NEXT ESTIMATE OF PO2
C ICH3=0 NO BRACKET AT THIS TIME
C SET MAGNITUDE INITIAL STEP LENGTHS FOR BRACKETING PROCEDURE TO FIND
C BOUNDS WITHIN WHICH PO2 AND PCO2 MUST LIE
C SET INITIAL FLAG VALUES AS ifCURRENT VALUES FOR PO2 AND PCO2
C WERE AS INTERPOLATED AT A BRACKET
      D1Z=2.
      D2Z=2.
      ICH1=1
      ICH2=1
      ICH3=1
      ICH4=1
C START SEARCH FROM CURRENT ESTIMATES OF PO2 AND PCO2
  100 CALL GASES(PO2,PC2,X,Y,PH,SAT)
C XX2/YY2 STORE DISCREPANCY BETWEEN KNOWN CONTENTS AND VALUES
C COMPUTED USING CURRENT GUESSES OF PRESSURES
      XX2=X-O2CON
      YY2=Y-C2CON
C AVOID ERROR IN SIGN FUNCTION BELOW
      IF(XX2) 120,110,120
  110 XX2=.001
  120 IF(YY2) 140,130,140
  130 YY2=.001
C XP2/YP2 STORE PRESSURE ESTIMATES CORRESPONDING TO DISCREPANCIES
C XX2/YY2. THESE ARE ALL USED IN INTERPOLATION PROCEDURE BELOW.
C FORM NEW SEARCH VECTORS D1/D2, AFTER BRACKETING AND
C INTERPOLATION, ACCORDING TO SIGN OF DISCREPANCIES XX2/YY2.
C RESET ICH3/ICH4 TO ZERO
  140 XP2=PO2
      YP2=PC2
      IF(ICH3-1)160,150,160
  150 ICH3=0
      D1=SIGN(D1Z,-XX2)
  160 IF(ICH4-1)210,170,210
  170 ICH4=0
      D2=SIGN(D2Z,-YY2)
C TEST FOR CONVERGENCE OF CALCULATED AND GIVEN CONTENTS.  SET FLAGS
C ICH1/ICH2 TO ZERO ifCONTENTS APPROXIMATED WELL ENOUGH
      IF(ABS(XX2)-ERR) 180,180,190
  180 ICH1=0
  190 IF(ABS(YY2)-ERR) 200,200,210
  200 ICH2=0
C FINISH ifBOTH CONTENTS APPROXIMATED WELL ENOUGH
      IF(ICH1+ICH2)450,450,210
C COMPUTE TRIAL INCREMENT DS FOR PO2 ENFORCING A LIMIT OF
C -PO2*.75 ifIT IS NEGATIVE. (STOP NEGATIVE TRIAL
C VALUES AND SMOOTH APPROACH TO A SOLUTION).
  210 DS=D1*ICH1
      X=PO2+DS-PO2*.25
      if(X) 220,230,230
  220 DS=-PO2*.75
  230 PO2=PO2+DS
C COMPUTE TRIAL INCREMENT OF PCO2 AS FOR PO2 ABOVE
      DS=D2*ICH2
      X=PC2+DS-PC2*.25
      if(X) 240,250,250
  240 DS=-PC2*.75
  250 PC2=PC2+DS
C ENFORCE LOWER BOUND ON PO2/PCO2
      IF(PO2-.1) 260,270,270
  260 PO2=.1
  270 IF(PC2-.1)280,290,290
  280 PC2=.1
C COMPUTE CONTENTS WITH TRIAL INCREMENTS MADE TO PO2/PCO2
  290 CALL GASES(PO2,PC2,X,Y,PH,SAT)
C SAVE LAST BUT ONE PAIRS OF VALUES PO2/O2CON DISCREPANCY IN
C XX1/XP1, SIMILARLY FOR PC2/C2CON DISCREPANCY IN YY1/YP1.
C THEN PLACE LATEST PAIRS OF VALUES IN XX2/XP2, YY2/YP2 AS BEFORE
      XX1=XX2
      XX2=X-O2CON
      XP1=XP2
      XP2=PO2
      YY1=YY2
      YY2=Y-C2CON
      YP1=YP2
      YP2=PC2
C FIRST LOOK AT DISCREPANCY IN O2CON (ICH1=0). ifWITHIN LIMIT
C 'ERR' ACCEPT VALUES OF PO2 AND LOOK AT C2CON
      IF(ABS(XX2)-ERR) 300,300,310
  300 ICH1=0
      GOTO 360
C ifO2CON DISCREPANCY STILL TOO HIGH TEST WHETHER TRIAL PO2 HAS
C OVERSHOT THE CORRECT SOLUTION
  310 ICH1=1
      IF(XX2*D1) 320,360,350
C ifNOT OVERSHOT, SOLUTION LIES FURTHER ALONG DIRECTION OF TRIAL
C INCREMENT.  COMPUTE A NEW TRIAL INCREMENT BASED ON LAST 2 PAIRS
C OF CONTENTS/PRESSURES VALUES. FACTOR 'FACT' WAS USED IN
C IN OPTIMISING THE ALGORITHM IN PRACTICE
  320 IF(XP2-XP1) 340,330,340
  330 D1=SIGN(D1Z,-XX2)
      GO TO 360
  340 D1=(XP2-XP1)*ABS(XX2)/(FACT*ABS(XX2-XX1))
      GO TO 360
C ifOVERSHOT, (I.E. BRACKET FOUND ON PO2) LINEARLY INTERPOLATE
C USING LAST 2 PAIRS OF PRESSURES/CONTENTS VALUES TO GET NEW TRIAL
C VALUE FOR PO2. SET ICH3=1 TO INDICATE BRACKETED INTERPOLATED
C VALUE FOR NEW SEARCH TO BE INITIATED AT STATEMENT 100. REDUCE
C INITIAL SEARCH VECTOR TO AID CONVERGENCE
  350 ICH3=1
      PO2=XP1+(XP2-XP1)*ABS(XX1)/(ABS(XX2)+ABS(XX1))
      D1Z=D1Z/2.
C NOW REPEAT ABOVE PROCEDURE FOR DISCREPANCY IN C2CON
  360 IF(ABS(YY2)-ERR) 370,370,380
  370 ICH2=0
      GO TO 430
  380 ICH2=1
      IF(YY2*D2) 390,430,420
C KEEP GOING
  390 IF(YP2-YP1) 410,400,410
  400 D2=SIGN(D2Z,-YY2)
      GO TO 430
  410 D2=(YP2-YP1)*ABS(YY2)/(FACT*ABS(YY2-YY1))
      GO TO 430
C BRACKET FOUND
  420 ICH4=1
      PC2=YP1+(YP2-YP1)*ABS(YY1)/(ABS(YY1)+ABS(YY2))
      D2Z=D2Z/2.
C FINISH ifBOTH O2CON AND C2CON WELL ENOUGH APPROXIMATED.  ifEITHER
C PO2 OR PCO2 SEARCH IS AT A BRACKET ESTABLISH NEW SEARCH AT
C STATEMENT 100.  ELSE, ifBOTH ARE CONTINUING ALONG PREVIOUS
C DIRECTIONS OF SEARCH PROCEED FROM STATEMENT 210
  430 IF(ICH1+ICH2) 450,450,440
  440 IF(ICH3+ICH4-1) 210,100,100
  450 RETURN
      END

